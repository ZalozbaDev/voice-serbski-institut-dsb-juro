plugins {
    id 'de.undercouch.download' version '5.3.0'
    id 'de.dfki.mary.voicebuilding-legacy'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        force 'de.dfki.mary:marytts-lang-dsb:0.1.0'
    }
}

marytts {
    voice {
        name = 'serbski-institut-dsb-juro'
        gender = 'male'
        language = 'dsb'
        type = 'unit selection'
        samplingRate = 16000
    }
}

tasks.register('downloadData') {
    def flacFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('serbski-institut-dsb-data.flac'))
    def yamlFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('serbski-institut-dsb-data.yaml'))
    outputs.file flacFile
    outputs.file yamlFile
    doLast {
        def baseUrl = 'https://github.com/marytts/serbski-institut-dsb-data/releases/download/v0.1.0'
        download.run {
            src([
                    "$baseUrl/serbski-institut-dsb-data.flac",
                    "$baseUrl/serbski-institut-dsb-data.yaml"
            ])
            dest buildDir
            overwrite false
        }
    }
}

wav {
    srcDir = layout.projectDirectory.dir('wav')
}

basenames {
    textDir = layout.projectDirectory.dir('text')
    labDir = layout.projectDirectory.dir('lab')
    exclude 'Miks107_2',
            'Miks52'
}

generateAllophones {
    srcDir = layout.projectDirectory.dir('text')
}

processPhoneLabels {
    srcDir = layout.projectDirectory.dir('lab')
}
